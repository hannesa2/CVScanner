import org.gradle.internal.logging.text.StyledTextOutputFactory

import static org.gradle.internal.logging.text.StyledTextOutput.Style

plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    defaultConfig {
        applicationId "info.opencv.samples"
        minSdkVersion 21
        compileSdk 34
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments useTestStorageService: 'true'
    }

    buildFeatures {
        viewBinding = true
    }

    signingConfigs {
        def out = services.get(StyledTextOutputFactory).create("")
        if (System.env.USER == "hannes") {
            release {
                def keystore = "${project.gradle.gradleUserHomeDir}/../.android/debug.keystore"
                out.style(Style.Normal).text("I run with user hannes using ")
                        .style(Style.SuccessHeader).println(keystore)
                storePassword "android"
                keyPassword "android"
                keyAlias "androiddebugkey"
                storeFile file(keystore)
            }
        } else if (System.getenv("TRAVIS")) {
            release {
                out.style(Style.Normal).text("I run on TRAVIS")
                storeFile file('../signing/release.keystore')
                storePassword System.getenv("KEYSTORE_PASS")
                keyAlias System.getenv("ALIAS_NAME")
                keyPassword System.getenv("ALIAS_PASS")
            }
        } else {
            release {
                out.style(Style.Normal).text("I run on Github actions")
                storeFile file('../signing/release.keystore')
                storePassword System.getenv("KEYSTORE_PASS")
                keyAlias System.getenv("ALIAS_NAME")
                keyPassword System.getenv("ALIAS_PASS")
            }
        }
        debugCI {
            storeFile file('./debug.keystore')
            storePassword "android"
            keyPassword "android"
            keyAlias "androiddebugkey"
        }
    }

    buildTypes {
        release {
            if (!System.getenv("JITPACK")) {
                signingConfig signingConfigs.release
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        debug {
            if (System.getenv("CI") == "true") { // Github action
                signingConfig signingConfigs.debugCI
            }
            applicationIdSuffix ".debug"
            versionNameSuffix ".debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    namespace 'info.hannes.cvscanner.sample'

}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'com.github.AppDevNext.Logcat:LogcatCoreLib:3.2'

    implementation project(':library')
    implementation 'androidx.core:core-ktx:1.12.0'

    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.5"
    androidTestUtil "androidx.test.services:test-services:1.4.2"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"

    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.5.1'
    // RecyclerViewActions.actionOnItem
    androidTestImplementation "androidx.test:rules:1.5.0" //    GrantPermissionRule
    androidTestImplementation "androidx.test.espresso:espresso-intents:3.5.1"
}
